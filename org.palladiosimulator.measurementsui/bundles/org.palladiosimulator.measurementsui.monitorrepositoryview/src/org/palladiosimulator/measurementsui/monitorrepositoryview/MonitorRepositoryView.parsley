import java.util.ArrayList
import org.palladiosimulator.edp2.models.measuringpoint.MeasuringPoint
import org.palladiosimulator.monitorrepository.Monitor
import org.palladiosimulator.monitorrepository.MeasurementSpecification
import org.palladiosimulator.monitorrepository.MonitorRepository
import org.palladiosimulator.measurementsui.monitorrepositoryview.MonitorRepositorySaveableTreeFormView
import org.palladiosimulator.monitorrepository.ProcessingType
import org.palladiosimulator.measurementsui.wizardmodel.WizardModelType

/* mpview EMF Parsley Dsl Module file */
module monitorrepositoryview {
	parts {
		viewpart monitorrepositoryview {
			viewname "MonitorRepositoryView"
			viewclass MonitorRepositorySaveableTreeFormView
		}
	}
	
	viewerContentProvider {
		children {
			Monitor mon -> {
				new ArrayList() => [
					addAll(mon.measurementSpecifications)
				]
			}
			MeasuringPoint -> #[]
		}
	}

	labelProvider {
		text {
			Monitor mon -> {
				if (mon.measuringPoint !== null) {
					mon.entityName + " [" + mon.measuringPoint.toString.replaceAll("\\[[^\\[]*\\]$", "") + "]";
				} else {
					mon.entityName + " [There is no MeasuringPoint set!]";
				}
			}

			MeasurementSpecification ms -> {
				"MeasurementSpecification: " + ms.metricDescription.getName
			}
			ProcessingType proc -> {
				// Remove the "[TRANSIENT]" substring from the label 
				proc.toString.replaceAll("\\[[^\\[]*\\]$", "")
			}
		}

		image {
			Monitor mon -> if (mon.activated) {
				"ActiveIcon.png"
			} else {
				"Inactive.png"
			}
			MeasurementSpecification ms -> if (ms.isTriggersSelfAdaptations)
				"self_adaption.png"
			else
				"nonself_adaption.png"
			MeasuringPoint -> "MeasuringPoint.png"
			ProcessingType -> "ProcessingType.png"

		}
	}

	menuBuilder {
		menus {
			MonitorRepository mr -> #[
				actionChange(
					"Add Measuringpoint with Monitor",
					mr,
					[
						MonitorRepositorySaveableTreeFormView.openWizard();
					]
				),
				separator,
				actionUndo,
				actionRedo
			]
		}

		emfMenus{
			MonitorRepository mr -> #[]

			Monitor monitor -> #[actionChange(
				"Add Measuringpoint with Monitor",
				monitor,
				[
					MonitorRepositorySaveableTreeFormView.openWizard();
				]
			), submenu("Edit", #[
				actionChange(
					"Monitor",
					monitor,
					[
						MonitorRepositorySaveableTreeFormView.openWizard(WizardModelType.MONITOR_CREATION, monitor);
					]
				),
				actionChange(
					"Measuringpoint",
					monitor,
					[
						MonitorRepositorySaveableTreeFormView.openWizard(WizardModelType.MEASURING_POINT_SELECTION,
							monitor);
					]
				),
				actionChange(
					"Measurement Specifications",
					monitor,
					[
						MonitorRepositorySaveableTreeFormView.openWizard(WizardModelType.METRIC_DESCRIPTION_SELECTION,
							monitor);
					]
				)
			]), separator]

			MeasuringPoint measuringPoint -> #[
				actionChange(
					"Edit Measuringpoint",
					measuringPoint,
					[]
				),
				separator
			]

			MeasurementSpecification measurement -> #[
				submenu("Edit", #[
					actionChange(
						"MeasurementSpecifications",
						measurement,
						[
							val monitor = measurement.eContainer as Monitor
							MonitorRepositorySaveableTreeFormView.openWizard(
								WizardModelType.METRIC_DESCRIPTION_SELECTION, monitor);

						]
					),
					actionChange(
						"ProcessingTypes",
						measurement,
						[]
					)
				]),
				separator
			]

			ProcessingType processing -> #[
				actionChange(
					"Edit Processing Types",
					processing,
					[]
				),
				separator
			]
		}
	}
}